class PickNickBench : Interactable {

    override void postBeginPlay()
    {
        super.postBeginPlay();
        if(args[0] == 1)
        {
            A_SPAWNITEMEX("PickNickBenchDamaged");
            destroy();
        }
        useIcon = TexMan.CheckForTexture("XH_KICK");
    }

    override int takeSpecialDamage (Actor inflictor, Actor source, int damage, Name damagetype) {
		int res = Super.TakeSpecialDamage(inflictor, source, damage, damageType);
		A_PLAYSOUND("impact/crate", CHAN_AUTO, 1.0, pitch:frandom(0.9,1.16));
        return res;
	}

    default {
        health 10;
        mass 700;
        Painchance 255;
        pushfactor 0.1;
        height 25;
        +SOLID;
        +SHOOTABLE
        -NOBLOOD
        +USESPECIAL;
        +SelacoActor.DisallowGravityManipulation;
        tag "$OBJECT_PARKBENCH";
        SelacoActor.UseTag "$INTERACT_KICK";
        Activation THINGSPEC_DEFAULT;
        DamageFactor "BulletAlien", 10;
        Bloodtype "BulletImpactEffectWoodShrapnel", "BulletImpactEffectWoodShrapnel", "BulletImpactEffectWoodShrapnel";
        DeathSound "dest/crate";
    }
    states {
        Spawn:
            TNT1 A 0;
            VOXE L 35;
            VOXE L -1
            {
                ActivationType = THINGSPEC_Switch;
                sleepIndefinite();
            }
            stop;
        Pain:
            TNT1 A 0 A_PAIN();
            goto Spawn;
        Inactive:
        Active:
            TNT1 A 0
            {
                PlayerPawn pawn=players[0].mo;
                let d = Dawn(pawn);
                if(d.countinv("NoDawnArmor") == 1)
                {
                    d.A_PLAYSOUND("dawn/targetedMelee5", CHAN_AUTO, 1.0, 0, 0);
                    SetStateLabel("Pain.shotgun");
                }
                if(SelacoWeapon(d.player.ReadyWeapon)&& d.playerHasWeaponReady(1))
                {
                    bUseSpecial = false;
                    activationType = THINGSPEC_DEFAULT;
                    if(getCvar("g_interactionanimations") == 1)
                    {
                        d.GiveInventory("DawnKickFast", 1);
                    }
                }
                else
                {
                    setStateLabel("Spawn");
                }
            }
            VOXE L 10;
            TNT1 A 0
            {
                A_PLAYSOUND("impact/melee", CHAN_AUTO);
                setStateLabel("DeathKick");
            }
            TNT1 A 1;
        DeathKick:
        Death:
            TNT1 A 0;
            TNT1 A 0
            {
                A_PLAYSOUND("impact/lightwood", CHAN_AUTO, 0.8, 0, 0.95, pitch:1.2);
            }
            TNT1 AAAAAAAAAAAAAA 0 A_SPAWNITEMEX ("BrownCloud", frandom(-10,10), frandom(-10,10), frandom(0,24), 0, frandom(-0.3,0.3));
            TNT1 AAAAA 0 A_SPAWNITEMEX("PlankSpawnerSlow", frandom(-10,10), frandom(-10,10), frandom(0,24));
            TNT1 A 0 A_SPAWNITEMEX("PickNickBenchDamaged");
            stop;
        Death.ExplosiveImpact:
            TNT1 AAAAAAAAAAAAAA 0 A_SPAWNITEMEX ("BrownCloud", frandom(-10,10), frandom(-10,10), frandom(0,24), 0, frandom(-0.3,0.3));
            TNT1 A 0
            {
                A_PLAYSOUND("impact/lightwood", CHAN_AUTO, 1.0, 0, 0.95, pitch:1.2);
            }
            TNT1 AAAAAAAA 0 A_SPAWNITEMEX("PlankSpawnerSlow", frandom(-10,10), frandom(-10,10), frandom(0,24));
            TNT1 A 0 A_SPAWNITEMEX("PICKNICKBENCHPIECE1", 0, 15, 4, frandom(4,13), 0, frandom(6,15));
            TNT1 A 0 A_SPAWNITEMEX("PICKNICKBENCHPIECE2", 0, -15, 4, frandom(4,13), 0, frandom(6,15));
            stop;
    }
}

class PickNickBenchDamaged : PickNickBench {
    states {
        DeathKick:
            TNT1 A 0
            {
                A_PLAYSOUND("impact/lightwood", CHAN_AUTO, 1.0, 0, 0.95, pitch:frandom(0.85,1.0));
            }
            TNT1 AAAAAAAAAAAAAA 0 A_SPAWNITEMEX ("BrownCloud", frandom(-10,10), frandom(-10,10), frandom(0,24), 0, frandom(-0.3,0.3));
            TNT1 AAAAAAAAAAAAAAA 0 A_SPAWNITEMEX("PlankSpawnerSlow", frandom(-10,10), frandom(-30,30), frandom(0,24));
            TNT1 A 0 A_SPAWNITEMEX("PICKNICKBENCHPIECE1", 0, -15, 4, frandom(-1,1), frandom(-1,-2), frandom(4,5));
            TNT1 A 0 A_SPAWNITEMEX("PICKNICKBENCHPIECE2", 0, 15, 4, frandom(-1,1), frandom(1,2), frandom(4,5));
            stop;
        Death:
            TNT1 A 0
            {
                A_PLAYSOUND("impact/lightwood", CHAN_AUTO, 1.0, 0, 0.95, pitch:frandom(0.85,1.0));
            }
            TNT1 AAAAAAAAAAAAAA 0 A_SPAWNITEMEX ("BrownCloud", frandom(-10,10), frandom(-10,10), frandom(0,24), 0, frandom(-0.3,0.3));
            TNT1 AAAAAAAAAAAAAAA 0 A_SPAWNITEMEX("PlankSpawnerSlow", frandom(-10,10), frandom(-30,30), frandom(0,24));
            TNT1 A 0 A_SPAWNITEMEX("PICKNICKBENCHPIECE1", 0, -15, 4, frandom(-1,1), frandom(-2,-4), frandom(4,8));
            TNT1 A 0 A_SPAWNITEMEX("PICKNICKBENCHPIECE2", 0, 15, 4, frandom(-1,1), frandom(2,4), frandom(4,8));
            stop;
    }
}

class PICKNICKBENCHPIECE1 : SelacoActorDecorationMissile 
{
    // Hits floor
    override void bounceEffects()
    {
        for(int x=0;x<=3;x++) {
            A_SPAWNITEMEX("BodyImpactSmokeTiny", 0, 0, 0, frandom(-1,1), frandom(-1,1), frandom(0.4,1.7));
        }
        EmitDefinition('ShortSparkParticleDefinition', chance: 0.5, numTries: 12, angle:random(0,259), pitch:frandom(-25, -15), speed:-3, offset:(frandom(-3,3), frandom(-3,3),frandom(-3,3)), velocity:(frandom(0.4,1), frandom(0.4,1), frandom(1,4)));
        A_PlaySound("impact/metalsmall",CHAN_AUTO, 0.7, 0, 0.9, false, frandom(1.0,1.35));
    }

    default {
        DeathSound "";
        +MISSILE
        +DOOMBOUNCE
        +THRUACTORS;
        Gravity 0.7;
        Speed 3;
        Radius 5;
        Height 5;
        BounceFactor 0.2;
        BounceCount 3;
    }
    states {
        Spawn:
            TNT1 A 0;
        looper:
            VOXE L 3;
            TNT1 A 0 {
                A_SETANGLE(Angle + random(30,35));
                A_SETROLL(roll + random(30,35));
            }
            loop;
        Death:
            VOXE L -1 {
                A_SETROLL(0);
            }
            loop;
    }
}

class PICKNICKBENCHPIECE2 : PICKNICKBENCHPIECE1 {

}