/* These are sandbags. Maybe we'll expand upon this later on by having indivial sandbags that can leak!*/

class Palette : SelacoActor {


    override void BeginPlay() {
        super.BeginPlay();
        if (args[0] == 1) {
            frameID = random(0,2);
        }
    }

    int frameID;
	override int takeSpecialDamage (Actor inflictor, Actor source, int damage, Name damagetype) {
		int res = Super.TakeSpecialDamage(inflictor, source, damage, damageType);
        if(damage > 1 && frameID < 5) {
            height-=2;
            frameID++;
            if(frameID == 5)
            {
                for(int x=0;x<20;x++)
                {
                    A_SPAWNITEMEX("SmokeTrailPieceSmallWood", frandom(-45,45), frandom(-45,45), frandom(10,20));
                }
                health = 75;
                height-=4;
            }
        }
		return res;
	}

    default {
        +SHOOTABLE
        +NOBLOODDECALS;
        +CANPASS
        +SOLID;
        PainChance 255;
        Activation THINGSPEC_Default;
        scale 0.95;
        mass 1500;
        radius 26;
        height 29;
        MaxStepHeight 2;
        BloodType "PalettePain", "PalettePain", "PalettePain";
        PainSound "impact/grass";
    }
    States {
        spawn:
            VOXE A 1;
        Idle:
            VOXE A -1 {
                frame = frameID;
            }
            stop;
        Pain:
            TNT1 A 0;
            goto Idle;
        Pain.railattack:
        Pain.ExplosiveImpact:
        Death:
            TNT1 A 0
            {
                A_SPAWNITEMEX("Palette_piece_1", 25, 0, 6, frandom(1,3), 0, frandom(6,7), angle:120);
                A_SPAWNITEMEX("Palette_piece_2", 25, 25, 6, frandom(1,3), 0, frandom(6,7), angle:240);
                A_SPAWNITEMEX("Palette_piece_3", -12, -12, 6, frandom(1,3), 0, frandom(6,7), angle:360);
            }
            stop;
    }
}

class PalettePain : actor {
    mixin ParticleDefinitionEmitter;
    default {
        +NOINTERACTION
    }
    states {
        Spawn:
            TNT1 A 0;
            TNT1 A 0 {
                EmitDefinition('SawdustDefinition', chance: 0.75, numTries: 25, angle:-180, pitch: frandom(-10,10), offset: (20, 0, 0));	
            	A_SpawnProjectile ("WoodShrapnel", random(-2, 2), 0, random (-45, 45), 2, random(-35,35));
		        A_SpawnProjectile ("WoodShrapnel", random(-2, 2), 0, random (-45, 45), 2, random(-35,35));
            }
            stop;
    }
}

class Palette_piece_1 : SelacoActorDecorationMissile 
{ 
    override void bounceEffects()
    {
        A_PLAYSOUND("bounce/woodplank");
        A_SpawnProjectile ("SmokeTrailPieceSmallWood", random(-2, 2), 0, random (-45, 45), 2, random(-35,35));
		A_SpawnProjectile ("SmokeTrailPieceSmallWood", random(-2, 2), 0, random (-45, 45), 2, random(-35,35));
    }
}
class Palette_piece_2 : Palette_piece_1 { }
class Palette_piece_3 : Palette_piece_1 { }