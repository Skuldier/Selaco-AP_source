
// Converted to ZSCRIPT to have it detect walls and fall over when there is no wall near it
// Making this game objectively superior to Ion Fury in every way. 
// I'm kidding Voidpoint, I love you guys!
class broom_blue : Interactable {
    bool isDropped;
    void RandomImpactModifier() {
        int dice;
        dice = random(0,3);
        if (dice == 1) {
            A_SETANGLE(angle + 3);
        }
    }

    default {
        // FLAGS
        +ROLLSPRITE
        -BloodSplatter;
        +NOBLOODDECALS;
        +solid;
        +PUSHABLE;
        +Shootable;
        +Usespecial;
        -SELACOACTOR.SlopeAlignment
        +SelacoActor.WakeNearPlayer;
        SelacoActor.useTag "$INTERACT_PULL";

        // PROPERTY
        tag "$OBJECT_BROOM";
        Activation THINGSPEC_ACTIVATE;
        friction 0.8;
        mass 250;
        health 25;
        radius 8;
        speed 6;
        gravity 0.9;
        MaxStepHeight 6;
        height 55;
        scale 1.0;
        pushfactor 0.8;
        PainChance 255;
        BounceFactor 0.4;
        BounceCount 3;
        PainSound "impact/crate";
        DeathSound "impact/woodbreak";
        BloodType "BrownCloudParticles", "BrownCloudParticles", "BrownCloudParticles";

    }
    states {
        spawn:
            VOXE L 0;
            VOXE L 1 {
                FLineTraceData RemoteRay;
                LineTrace(angle - 180, 30, pitch,offsetz: height-2,data: RemoteRay);
                if (RemoteRay.HitType != TRACE_HitWall && !isDropped) {
                    isDropped = true;
                    SetStateLabel("DropDown");
                }
            }
            loop;
        Active:
            VOXE L 8 {
                A_CHANGEVELOCITY(9,0,0, CVF_RELATIVE);
            }
            goto Spawn;
        DropDown:
            VOXE L 0 {
                bUseSpecial = false;
                activationType = THINGSPEC_DEFAULT;
                bThruPlayers = true;
                isDropped = true;
            }
            VOXE LLLLLLLLLLLLL 1 {
                A_SETANGLE(angle+5.5);
                A_SETPITCH(pitch - 6);
            }
            TNT1 A 0 A_PlaySound("impact/broomhit");
            VOXE LLLL 1 A_SETPITCH(pitch + 3);
            VOXE LLLL 1 {
                A_SETPITCH(pitch - 3);
                RandomImpactModifier();
            }
            VOXE LL 1 A_SETPITCH(pitch + 2);
            VOXE LL 1 {
                A_SETPITCH(pitch - 2);
                A_SETSIZE(5, 10);
                RandomImpactModifier();
            }
            VOXE L -1;
        Destroy:
            stop;
        Death:
            TNT1 A 0 A_PLAYSOUND("impact/woodbreak", CHAN_AUTO, 1.0, 0, 0.6);
            TNT1 A 0 A_SPAWNITEMEX("broom_brk_stick", 0, 0, 20, frandom(1, 2), frandom(-0.2,0.2), frandom(2.5, 3.9));
            TNT1 A 0 A_SPAWNITEMEX("broom_brk", 0, 0, 20, frandom(-1, -2), frandom(-0.2,0.2), frandom(3.0, 4.9));
            stop;
    }
}

class broom_green : broom_blue { }

Class broom_brk_stick : SelacoActor {
    default {
        radius 5;
        height 5;
        speed 3;
        +missile;
        +doombounce;
        gravity 0.8;
        BounceFactor 0.3;
        BounceCount 3;
    }
    States {
        Spawn:
            VOXE L 3;
            TNT1 A 0 A_SetPitch(pitch + frandom(45,50));
            TNT1 A 0 A_SetAngle(angle + frandom(15,25));
            loop;
        Death:
            TNT1 A 0 A_SetPitch(0);
            VOXE L -1;
            stop;
    }
}

Class broom_brk : broom_brk_stick {

}
